#! /bin/csh -f

if ($#argv < 3) then
	echo "ERROR: usage: PM_cheq_deps <test_module_dir> <test_module_name> <installed_mods_dir>"
	exit 1
endif
set bindir = "$PAN_ROOT/PAN/public/bin"
set DEP = `$bindir/PM_extract_dep $1 $2`
if ("$DEP[1]" == "ERROR") then
	echo "$DEP in module $1/$2"
	exit 1
endif

if (! -e "$3") then
	echo "ERROR $3 does not exists"
	exit 1
endif

set req_mods = ""
while ("$DEP" != "")
	set module = $DEP[1]
	shift DEP
	set min_ver = $DEP[1]
	if ("$min_ver" == "") then
		echo "warning version for module $module not found. Assuming 0.0"
		set min_ver = "0.0"
	endif
	set inst_ver = `$bindir/PM_extract_version $3 $module`
	if ("$inst_ver[1]" == "ERROR") then
		set req_mods = $req_mods" "$module
	else
		set compar = `$bindir/PM_compare_versions $inst_ver $min_ver`
		if ("$compar" == "2") then
			set req_mods = $req_mods" "$module
		endif
	endif
	shift DEP
end

if ("$req_mods" == "") then
	echo "OK"
else
	echo $req_mods
endif


exit 0
